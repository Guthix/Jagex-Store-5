plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.11'
    id 'maven-publish'
    id 'java-library'
}

group 'io.github.bartvhelvert.jagex'
version '0.1-DEV'
sourceCompatibility = 1.9

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8'
    implementation group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: bouncyCastleVersion
    implementation group: 'org.apache.commons', name: 'commons-compress', version: apacheCompressVersion
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitVersion
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: junitVersion
    testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitVersion
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                name = 'Jagex-Filesystem'
                description = 'Jagex Filesystem library'
            }
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

test {
    useJUnitPlatform()
}