/*
 * Copyright (C) 2019 Guthix
 *
 * This program is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Lesser General Public
 * License as published by the Free Software Foundation; either
 * version 3 of the License, or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public License
 * along with this program; if not, write to the Free Software Foundation,
 * Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301, USA.
 */
plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.50' apply false
    id 'java-library'
    id 'maven-publish'
}

group 'io.guthix.cache'
version '0.2.6'

ext {
    jagexByteBufCommit = 'ff61704caa'
    kotlinLoggingVersion = '1.7.6'
    logbackVersion = '1.2.3'
    kotlintestVersion = '3.4.2'
    xzVersion = '1.8'
    bouncyCastleVersion = '1.63'
    apacheCompressVersion = '1.19'
}

allprojects {
    apply plugin: "org.jetbrains.kotlin.jvm"

    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    compileKotlin {
        sourceCompatibility = JavaVersion.VERSION_11
        kotlinOptions.jvmTarget = JavaVersion.VERSION_11
    }

    compileTestKotlin {
        sourceCompatibility = JavaVersion.VERSION_11
        kotlinOptions.jvmTarget = JavaVersion.VERSION_11
    }
}

test {
    useJUnitPlatform()
}

dependencies {
    api group: 'com.github.guthix', name: 'Jagex-ByteBuf', version: jagexByteBufCommit
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8'
    implementation group: 'org.tukaani', name: 'xz', version: xzVersion
    implementation group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: bouncyCastleVersion
    implementation group: 'org.apache.commons', name: 'commons-compress', version: apacheCompressVersion
    implementation group: 'io.github.microutils', name: 'kotlin-logging', version: kotlinLoggingVersion
    testImplementation group: 'ch.qos.logback', name: 'logback-classic', version: logbackVersion
    testImplementation group: 'io.kotlintest', name: 'kotlintest-runner-junit5', version: kotlintestVersion
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        register(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'Jagex-Store-5'
                description = 'A library for modifying Jagex Store 5 caches'
                url = "https://github.com/guthix/Jagex-Store-5.git"
                licenses {
                    license {
                        name = 'GNU Lesser General Public License v3.0'
                        url = 'https://www.gnu.org/licenses/lgpl-3.0.txt'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = "Github"
            url = uri("https://maven.pkg.github.com/guthix/Jagex-Store-5")
            credentials {
                username = findProperty("github.username")
                password = findProperty("github.token")
            }
        }
    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

